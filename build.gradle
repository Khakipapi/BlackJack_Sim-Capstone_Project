// References:
//	Getting Started with JavaFX - https://openjfx.io/openjfx-docs/#IDE-Eclipse
//	build.gradle - https://github.com/openjfx/samples/blob/master/IDE/Eclipse/Modular/Gradle/hellofx/build.gradle

plugins {
	id 'application'
	id 'eclipse'
	id 'org.openjfx.javafxplugin' version '0.1.0'
	id 'org.beryx.jlink' version '2.12.0'
}

repositories {
    mavenCentral()
}

dependencies {
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:win"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:linux"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac"
    
    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
	implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.44.0.0'
    
}

javafx {
    version = "21.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

mainClassName = "project.MainApp"

eclipse {
    classpath {
        file {
            whenMerged {
                entries.findAll { it.properties.kind.equals('lib') }.each {
                    it.entryAttributes['module'] = 'true'
                }
            }
        }
    }
}

jlink {
//    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Blackjack'
    }
}

jar {
	destinationDirectory.set(file("$buildDir/RunnableJAR"))
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'project.Launcher'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
